plugins {
  id 'com.android.library'
  id 'org.jetbrains.kotlin.android'
  id 'maven-publish'
  id 'signing'
  id 'jacoco'
}
def getArtificatId = { ->
  return "wallet-sdk"
}
def getVersionName = { ->
  new File(rootDir.getAbsolutePath() + '/version').text.replace("\n", "")
}

// Test Report
apply from: '../jacoco/modules.gradle'


def snapshotRepositoryUrl = hasProperty('SNAPSHOT_REPOSITORY_URL') ? SNAPSHOT_REPOSITORY_URL
    : "https://s01.oss.sonatype.org/content/repositories/snapshots/"

def repositoryUsername = project.findProperty("nexus.username") ?: hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : ""

def repositoryPassword = project.findProperty("nexus.pw") ?: hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : ""

android {
  compileSdkVersion rootProject.ext.android["compileSdkVersion"]
  defaultConfig {
    minSdkVersion rootProject.ext.android["minSdkVersion"]
    targetSdkVersion rootProject.ext.android["targetSdkVersion"]
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  buildFeatures {
    viewBinding true
  }
  testOptions {
    unitTests.all {
      jacoco {
        includeNoLocationClasses = true
      }
    }
    unitTests.returnDefaultValues = true
  }

  buildTypes {
    debug {
      testCoverageEnabled true
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  packagingOptions {
    exclude 'META-INF/rxjava.properties'
    exclude 'lib/x86/*.so'
    exclude 'lib/x86_64/*.so'
  }
}

dependencies {
  api(rootProject.ext.dependencies["bitcoinj-core"]) {
    exclude group: 'com.google.protobuf'
  }
  api(rootProject.ext.dependencies["web3j"])
  api(rootProject.ext.dependencies["gson"])
  api(rootProject.ext.dependencies["guava"])
  api 'com.google.protobuf:protobuf-kotlin-lite:3.19.4'
  api('com.google.crypto.tink:tink-android:1.6.1') {
    exclude module: 'protobuf-lite'
  }
  implementation(rootProject.ext.dependencies["semver"])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  testImplementation 'junit:junit:4.13.2'
}

task sourceJar(type: Jar) {
  from android.sourceSets.main.java.srcDirs
  archiveClassifier.set("sources")
}

signing {
  sign publishing.publications
}

publishing {
  repositories {
    maven {
      url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2")
//      url = uri(snapshotRepositoryUrl)
      credentials {
        username = repositoryUsername
        password = repositoryPassword
      }
    }
  }
  publications {
    maven(MavenPublication) {
      groupId 'io.arcblock.did'
      artifactId getArtificatId()
      version getVersionName()
      artifact("$buildDir/outputs/aar/${getArtificatId()}-release.aar")
      pom {
        name = artifactId
        description = 'A android library for generating DID identicons.'
        url = 'https://github.com/ArcBlock/did-wallet-sdk-android'
        licenses {
          license {
            name = 'The Apache Software License, Version 2.0'
            url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }
        developers {
          developer {
            id = 'ArcBlock'
            name = 'ArcBlock'
            email = 'app@arcblock.io'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/ArcBlock/did-wallet-sdk-android.git'
          developerConnection = 'scm:git:ssh://git@github.com/ArcBlock/did-wallet-sdk-android.git'
          url = 'https://github.com/ArcBlock/did-wallet-sdk-android'
        }

      }
    }
  }
}